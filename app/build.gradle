buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}

apply plugin: 'android'

dependencies {
    compile project(":core")
    compile 'com.novoda.imageloader:imageloader-core:1.6.+'
    instrumentTestCompile "com.google.android:android-espresso-bundled:1.0.0-SNAPSHOT"
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        buildConfigField "retrofit.client.Client", "HTTP_CLIENT", "new retrofit.client.UrlConnectionClient()"
    }

    deviceProvider new GenymotionDeviceProvider(project.plugins.findPlugin('android').getSdkParser())

    buildTypes {
        test {
            buildConfigField "retrofit.client.Client", "HTTP_CLIENT", "new com.xrigau.hnandroid.core.debug.MockClient()"
        }
    }
}

class GenymotionDeviceProvider extends com.android.builder.testing.ConnectedDeviceProvider {
    GenymotionDeviceProvider(com.android.builder.SdkParser sdkParser) {
        super(sdkParser)
    }

    public String getName() {
        return "genymotion" // Run with 'gradle genymotionInstrumentTest'
    }

    public List<? extends com.android.builder.testing.api.DeviceConnector> getDevices() {
        List localDevices = super.getDevices()
        def matchingDevices = localDevices.grep {
            it.getApiLevel() > 16 &&
                    it.iDevice.getProperty("ro.product.manufacturer") == 'Genymotion'
        }

        if (matchingDevices.size() == 0) {
            throw new IllegalArgumentException("Couldn't find any connected device that matches [${getName()}] provider.")
        }
        matchingDevices
    }
}